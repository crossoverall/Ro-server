//===== eAthena Script =======================================
//= Al De Baran Kafras
//===== By: ============================
//= eAthena Dev Team
//===== Current Version: =======================
//= 2.3a
//===== Compatible With: ============================
//= eAthena 1.0
//===== Description: ============================================
//= Description of argument settings for callfunc "F_Kafra".
//=  arg(0): When set at 0 the default Kafra message is displayed.
//=    When set to 1 the Niflhiem Kafra message is displayed.
//=    When set to 2 the Guild Kafra message is displayed. 
//=  arg(1): Set to 1 to disable teleport menu option. Otherwise set to 0.
//=  arg(2): Set to 1 to disable info menu.  Otherwise set to 0.
//=  arg(3): Cost of Storage service
//=  arg(4): Cost of Rent a Pushcart service
//===== Additional Comments: ==========================================
//= v1.1 Now using functions :)
//= v2.1b Minor changes to function calls. Using arguments.
//= This version uses arrays .[kobra_k88]
//= 2.2 New teleport destinations, prices [Lupus]
//= 2.3 Updated function calls with new arguments. [L0ne_W0lf]
//=	Kafra Leilah is now a full NPC and no longer uses functions.
//=	Added unique Kafra Welcome message.
//=	Changed "Yuno" to "Juno"
//= 2.3a Added "callfunc("F_CheckKafCode")" when you open storage. (bugreport:515) [Samuray22]
//=====================================================================

// Kafra Main Office (Kafra Leilah) ------------------------------------
aldeba_in,96,181,4	script	Kafra Service	113,{
	cutin "kafra_05",2;
	mes "[Kafra Leilah]";
	mes " ยินดีต้อนรับสู่บริษัท Kafra";
	mes "เราจะอยู่กับท่านทุกที่ที่ท่านไปถึง";
	mes " มีอะไรให้ข้าช่วยท่านได้บ้าง?";
	next;
	switch(select("เซฟ:เก็บของ:เช่ารถเข็น:ยกเลิก")) {
	case 1:
		mes "[Kafra Leilah]";
		mes "จุดเซฟได้ถูกบันทึก";
		savepoint "aldeba_in",96,179;
		close2;
		break;
	case 2:
		if (zeny < 20) {
			mes "[Kafra Leilah]";
			mes " ท่านต้องมีอย่างน้อย 20 Zeny";
			close;
		}
		mes "[Kafra Leilah]";
		mes "ขอบคุณที่ใช้ในบริการของเรา";
		mes "บริการของ Kafra Staff";
		mes "จะทำให้ลูกค้ามีความพึงพอใจที่สุด";
		next;
		set zeny,zeny-20;
		set RESRVPTS, RESRVPTS + (20/5);
		mes "[Kafra Leilah]";
		mes "ข้าจะเปิดคลังเก็บของ";
		mes "ให้กับท่านในตอนนี้";
		callfunc("F_CheckKafCode");    //check your storage password, if set
		close2;
		openstorage;
		break;
	case 3:
		mes "[Kafra Leilah]";
		mes "ข้าเสียใจ";
		mes "ตอนนี้ข้ายังไม่มีบริการนั้น";
		next;
		mes "[Kafra Leilah]";
		mes "เจ้าสามารถใช้บริการเช่ารถเข็น";
		mes "ได้ที่ Kafra ท่านอื่นๆ";
		mes "ใน Rune Midgard";
		close2;
		break;
	case 4:
		mes "[Kafra Leilah]";
		mes "ขอบคุณที่ใช้บริการ Kafra";
		close2;
		break;
	}
	cutin "",255;
	end;
}

// Central --------------------------------------------------
aldebaran,143,119,4	script	Kafra Staff::kaf_aldebaran	113,{
	cutin "kafra_05",2;
	cleararray @wrpC$[0],"",getarraysize(@wrpC$);
	setarray @wrpP[0], 2400, 2400, 3600, 3400;
	setarray @wrpD$[0], "Geffen", "Juno" , "Izlude", "Mjolnir Dead Pit";
	set @wrpC$[0], @wrpD$[0]+" -> "+@wrpP[0];
	set @wrpC$[1], @wrpD$[1]+" -> "+@wrpP[1];
	set @wrpC$[2], @wrpD$[2]+" -> "+@wrpP[2];
	set @wrpC$[3], @wrpD$[3]+" -> "+@wrpP[3];
	set @wrpC$[4], "Cancel";
	mes "[Kafra Staff]";
	mes " ยินดีต้อนรับสู่บริษัท Kafra";
	mes "เราจะอยู่กับท่านทุกที่ที่ท่านไปถึง";
	mes " มีอะไรให้ข้าช่วยท่านได้บ้าง?";
	callfunc "F_Kafra",5,0,1,20,600;

	M_Save:
		savepoint "aldebaran",143,109;
		callfunc "F_KafEnd",0,1,"in the city of Al De Baran";
}
