payon_in02,64,71,4	script	สมาคม Archer#archer	85,{
	if (Upper == 1) {
		if (Class == Job_Novice_High && (ADVJOB == Job_Sniper || ADVJOB == Job_Clown || ADVJOB == Job_Gypsy)) {
			mes "[สมาคม Archer]";
			mes " เฮ้ ข้ารู้ว่าเจ้าต้องกลับมาเพื่อรับการทดสอบอีกครั้ง";
			next;
			mes "[สมาคม Archer]";
			mes " อ่า, แต่เจ้าจะต้องไปเริ่มต้นการผจญภัยใหม่ด้วยตัวเจ้าอีกรอบ";
			mes " ว้าววว ข้าว่ามันน่าตื่นเต้นน่าดู";
			next;
			if (getskilllv("NV_BASIC") < 9) {
				mes "[สมาคม Archer]";
				mes " เอ่อ...";
				mes " แต่เจ้ายังเรียน Basic SKil ไม่ครบ";
				next;
				mes "[สมาคม Archer]";
				mes " ดีล่ะ ไม่เจอกันคราวหน้า";
				close;
			}
			mes "[สมาคม Archer]";
			mes " เยี่ยมมม ข้าคงไม่มีอะไรจะสอนเจ้าอีกแล้ว";
			mes " เพราะเจ้าได้ผ่านอะไรมามากมาย จนพร้อมที่จะเป็น Archer อย่างเต็มตัว";
			next;
			skill 143,0,0;
			jobchange Job_Archer_high;
			skill 147,1,0;
			skill 148,1,0;
			mes "[สมาคม Archer]";
			mes " ครั้งนี้อาจไม่มีอะไรพิเศษ";
			mes " ที่ข้าจะมอบให้กับเจ้า แต่ข้าหวังว่าเจ้าคงจะเข้าใจข้านะ";
			close;
		}
		else {
			mes "[สมาคม Archer]";
			mes " โอ้...?";
			mes " เฮ้ นี่อะไร";
			mes " เจ้ามาทำอะไรที่นี่...?";
			close;
		}
	}
	mes "[สมาคม Archer]";
	mes " วันนี้เป็นวันดี เจ้ามีอะไรไรให้ข้าช่วย?";
	next;
	switch(select(" ข้าต้องการเป็น Archer: ข้าต้องการเคล็ดลับที่จำเป็น: ไม่หล่ะ ขอบคุณ")) {
	case 1:
		if (BaseJob == Job_Archer) {
			mes "[สมาคม Archer]";
			mes " เจ้าเป็น Archer อยู่แล้ว!!";
			close;
		}
		else if (BaseJob != Job_Archer && BaseJob != Job_Novice) {
			mes "[สมาคม Archer]";
			mes "หืม.....";
			mes " เจ้าไม่ใช่ Novice แล้วเจ้ามาทำอะไรที่นี่";
			next;
			mes "[สมาคม Archer]";
			mes " ข้าไม่สามารถทำในสิ่งที่เจ้าต้องการได้ เนื่องจากเจ้ามีอาชีพที่เจ้าต้องการอยู่แล้ว";
			close;
		}
		if (job_archer_q == 0) {
			mes "[สมาคม Archer]";
			mes " เจ้าต้องการที่จะเป็น Archer แล้วใช่มั๊ย?";
			mes " ถ้าอย่างนั้น เจ้าต้องกรอกใบสมัครนี่้";
			next;
			if (select(" ใช่ ตกลง: ยกเลิก") == 1) {
				set job_archer_q,1;
				setquest 1004;
				mes "[สมาคม Archer]";
				mes " เอาล่ะ ก่อนที่จะเข้าสู่การเป็น Archer ข้าต้องมีบททดสอบแก่เจ้าเสียก่อน 1 อย่าง";
				next;
				mes "[สมาคม Archer]";
				mes " นี่คือภารกิจของเจ้าเจ้าต้องนำ Trunk แต่ละชนิด มาให้ข้าให้ครบจำนวน 25 คะแนน";
				mes " ถ้าน้อยกว่านั้น ข้าจะไม่สามารถเปลี่ยน Archer ให้กับเจ้าได้";
				mes " ซึ่ง Trunk แต่ละชนิดนะจะสามารถหาได้จาก Willow รอบๆ เมือง Payon";
				next;
				mes "[สมาคม Archer]";
				mes " นี่คือคะแนนของไม้แต่ละชนิดที่จะได้รับ!!";
			mes " Fine-grained Trunk = 5 คะแนน";
			mes " Soul Trunk            = 3 คะแนน";
			mes " Barren Trunk         = 2 คะแนน";
			mes " Trunk                   = 1 คะแนน";
				mes " เจ้าคงพร้อมแล้วใช่มั๊ย!!";
				next;
				if (select(" ใช่ พร้อมแล้ว: ไม่ ข้าไม่พร้อม") == 1) {
					mes "[สมาคม Archer]";
					mes " เอาหล่ะ เริ่มตรวจสอบ";
					next;
				}
				else {
					mes "[สมาคม Archer]";
					mes " ข้าเข้าใจว่าเจ้าเป็นแขกของข้า แต่ข้าก็ต้องตรวจสอบเจ้าก่อนอยู่ดี";
					close;
				}
			}
			mes "[สมาคม Archer]";
			mes " Ok เจ้าผ่านได้";
			mes " เจ้าเริ่มทำภารกิจของเจ้าได้เลย";
			close;
		}
		mes "[สมาคม Archer]";
		mes " เจ้าคือ ... " + strcharinfo(0) + "?";
		next;
		if (getskilllv("NV_BASIC") < 9) {
			mes "[สมาคม Archer]";
			mes " เจ้ายังมีสกิลพื้นฐานไม่ครบ เจ้าควรกลับมาใหม่เมื่อพร้อม";
			next;
			mes "[สมาคม Archer]";
			mes " ระดับเลเวลจ๊อบของเจ้ายังไม่ถึง 10 ข้าว่าเจ้าควรไปฝึกฝนให้ดีกว่านี้ก่อนนะ";
			close;
		}
		if (job_archer_q == 1) {
			set .@archer_item1,countitem(1066) * 5;
			set .@archer_item2,countitem(1067) * 3;
			set .@archer_item3,countitem(1068) * 2;
			set .@archer_item4,countitem(1019);
			set .@total_archer,.@archer_item1 + .@archer_item2 + .@archer_item3 + .@archer_item4;
			set .@total_archer2,(((.@archer_item2 + .@archer_item3) * 2) + .@archer_item4);
			mes "[สมาคม Archer]";
			mes " เยี่ยมยอด!";
			mes " เจ้ากลับมาแล้ว..";
			mes " ขอดู... Trunk ที่เจ้าหามาหน่อยซิ";
			next;
			mes "[สมาคม Archer]";
			mes " Fine-grained Trunk = 5 คะแนน";
			mes " Soul Trunk            = 3 คะแนน";
			mes " Barren Trunk         = 2 คะแนน";
			mes " Trunk                   = 1 คะแนน";
			mes " นี่คือสิ่งที่ข้าต้องการ";
			next;
			if (countitem(1066) == 0 && countitem(1067) == 0 && countitem(1068) == 0 && countitem(1019) == 0) {
				mes "[สมาคม Archer]";
				mes " อื้มมม...";
				mes " นี่เจ้าไม่มีคะแนนเลย เจ้ายังไม่ได้รวบรวม Trunk จากเจ้า Willow ที่ข้าต้องการมาให้ข้าเลยใช่มั้ย?";
				close;
			}
			mes "[สมาคม Archer]";
			mes " คะแนนของเจ้า";
			if (countitem(1066) != 0) mes " เกรด S : " + countitem(1066) + " อัน / : " + .@archer_item1 + " . ";
			if (countitem(1067) != 0) mes " เกรด A : " + countitem(1067) + " อัน / : " + .@archer_item2 + " . ";
			if (countitem(1068) != 0) mes " เกรด B : " + countitem(1068) + " อัน / : " + .@archer_item3 + " . ";
			if (countitem(1019) != 0) mes " เกรด C : " + countitem(1019) + " อัน / : " + .@archer_item4 + " . ";
			if (.@total_archer < 25) {
				mes " รวม : ^FF0000" + .@total_archer + "^000000 / 40";
				next;
				mes "[สมาคม Archer]";
				mes " คะแนนของเจ้าต่ำกว่า 25 เจ้าต้องมีคะแนนอย่างน้อย 25 คะแนนนะ";
				close;
			}
			else {
				mes " รวม : ^0000FF" + .@total_archer + "^000000 / 40";
				next;
				mes "[สมาคม Archer]";
				if (.@total_archer > 40) {
					mes " ว้าววว! เจ้ามีคะแนนมากกว่า 40!";
					mes " เจ้าทำได้ยอดเยี่ยมมากจริงๆ!";
				}
				else if (.@total_archer > 30) {
					mes " เจ้ามีคะแนนมากกว่า 30! เจ้าทำได้ดี!";
					mes " ยินดีด้วย!";
				}
				else {
					mes " *เฮ้อ* ก็ยังดี, ที่เจ้ายังทำได้แค่ผ่านเท่านั้น!!";
				}
			}
			next;
			mes "[สมาคม Archer]";
			mes " ข้าจะส่งไม้พวกนี้ไปยังฝ่ายผลิตของเรา ตอนนี้เจ้าได้ทำภารกิจสำเร็จแล้ว";
			if (countitem(1066) != 0) delitem 1066,countitem(1066); //Tree_Of_Archer_1
			if (countitem(1067) != 0) delitem 1067,countitem(1067); //Tree_Of_Archer_2
			if (countitem(1068) != 0) delitem 1068,countitem(1068); //Tree_Of_Archer_3
			if (countitem(1019) != 0) delitem 1019,countitem(1019); //Wooden_Block
		}
		next;
		callfunc "Job_Change",Job_Archer;
		callfunc "F_ClearJobVar";
		completequest 1004;
		mes "[สมาคม Archer]";
		mes " ยินดีด้วย!";
		mes " ตอนนี้เจ้าคือ Archer!";
		next;
		mes "[สมาคม Archer]";
		mes " แน่นอนข้าหวังว่าเจ้าคงจะทำประโยชน์เพื่อสมาคมของเราบ้าง";
		next;
		mes "[สมาคม Archer]";
		mes " อ่า ธนูที่ส่งมาจากฝ่ายผลิตมาถึงพอดี ข้ามอบให้ท่าน~";
		getitem 1702,1; //Bow_
		getitem 1750,.@total_archer2; //Arrow
		next;
		mes "[สมาคม Archer]";
		mes " ตอนนี้ท่านฝึกฝนสำเร็จแล้ว ยินดีกับท่านด้วยอีกครั้ง!";
		close;
	case 2:
		mes "[สมาคม Archer]";
		mes " ข้าจะอธิบายเกี่ยวกับการเป็น Archer ให้เจ้าฟัง";
		if (BaseJob != Job_Novice) {
			if (BaseJob == Job_Archer) {
				next;
				mes "[สมาคม Archer]";
				mes " แต่...";
				mes " เจ้าก็เป็น Archer อยู่แล้ว แล้วเจ้ายังจะมาฟังข้าอีกทำไม...";
			}
			else {
				next;
				mes "[สมาคม Archer]";
				mes " เจ้ามีงานอื่นที่ต้องทำ ข้าว่าท่านไม่จำเป็นต้องรู้หรอกนะ~";
			}
			mes " ข้าไม่มีเหตุผลที่จะอธิบายให้เจ้าฟัง";
		}
		next;
		mes "[สมาคม Archer]";
		mes " ขั้นแรกเจ้าต้องฝึกฝนจนเจ้ามีจ๊อบเลเวล 9 และ เรียน Basic Skill จนครบ";
		next;
		mes "[สมาคม Archer]";
		mes " เนื่องจากอาชีพ Archer นั้นเป็นอาชีพที่หลายๆ คนก็ไฝ่ฝัน ข้าจึงต้องการให้พวกเจ้าผ่านบททดสอบนี้ไปก่อน";
		next;
		mes "[สมาคม Archer]";
		mes " ขั้นต่อมาเจ้าต้องรวบรวมคะแนนจาก ^FF0000Trunks^000000 ที่มีอยู่ 4 ชนิดในธรรมชาติซึ่งมีคุณสมบัติและคุณภาพที่แตกต่างกัน";
		next;
		mes "[สมาคม Archer]";
		mes " เพื่อที่เจ้าจะได้เป็น Archer เจ้าต้องมีคะแนนอย่างน้อย ^0000FF25^000000 คะแนน จนถึง 40 คะแนน ไม้ทั้งหมดจะหาได้จาก 'Willow' เจ้าควรจะต้องใช้ความระมัดระวัง";
		close;
	case 3:
		close;
	}
}

